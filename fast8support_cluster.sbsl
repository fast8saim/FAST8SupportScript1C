@Глобально
метод ЗавершитьСеансы(ПараметрыПодключения: Соответствие<Строка, Строка>)

    пер Сервер = новый АдминистрированиеСервераV8(ПараметрыПодключения["АдресСервера"], 1545)
    пер ТекущийКластер = Сервер.ПолучитьКластеры()[0]

    ТекущийКластер.ВыполнитьАутентификацию("", "")
    пер СписокИБ = ТекущийКластер.ПолучитьИнфобазы()
    для текИБ из СписокИБ
        если текИБ.Имя == ПараметрыПодключения["ИмяБазы"]
            пер База = текИБ.ВыполнитьАутентификацию(ПараметрыПодключения["Пользователь"], ПараметрыПодключения["Пароль"])
            База.ВключенаБлокировкаНачалаСеансов = Истина
            База.ВремяНачалаБлокировки = новый Момент(0, 0, 0, ЧасовойПояс.Текущий())
            База.ВремяОкончанияБлокировки = новый Момент(0, 0, 0, ЧасовойПояс.Текущий())
            База.КодРазрешенияНачалаСеансов = ПараметрыПодключения["КодРазрешения"]
            База.СообщениеБлокировки = "Обслуживание базы"
            База.Записать()
            пер Сеансы = База.ПолучитьСеансы()
            для сеанс из Сеансы
                если сеанс.ИмяПриложения != "RAS"
                    Консоль.Записать("завершен " + сеанс.ИмяПользователя + " " + сеанс.ИмяПриложения)
                    сеанс.ЗавершитьСеанс()
                ;
            ;
        ;

    ;
;