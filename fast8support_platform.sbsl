@Глобально
метод ВывестиСписокРасширений(ПараметрыПодключения: Соответствие<Строка, Строка>)
    
    пер ОткрыватьКонфигуратор: Булево = Истина
    пер ИмяОперации: Строка = "Вывод списка расширений"

    пер ПараметрыЗапуска = ПолучитьПараметрыЗапуска(ПараметрыПодключения, ОткрыватьКонфигуратор, ИмяОперации)
    
    пер ПараметрыПлатформы = ПолучитьПараметрыПлатформы(ОткрыватьКонфигуратор, ПараметрыПодключения)
    ПараметрыПлатформы.Добавить("/DumpDBCfgList -AllExtensions")
    
    ЗапуститьПлатформу(ПараметрыЗапуска, ПараметрыПлатформы)
;

@Глобально
метод УдалитьПатчи(ПараметрыПодключения: Соответствие<Строка, Строка>)
    
    пер ОткрыватьКонфигуратор: Булево = Ложь
    пер ИмяОперации: Строка = "Удаление патчей релиза"

    пер ПараметрыЗапуска = ПолучитьПараметрыЗапуска(ПараметрыПодключения, ОткрыватьКонфигуратор, ИмяОперации)
    
    пер ПараметрыПлатформы = ПолучитьПараметрыПлатформы(ОткрыватьКонфигуратор, ПараметрыПодключения)
    ПараметрыПлатформы.Добавить("/Execute fast8UpdateHelper.epf")
    ПараметрыПлатформы.Добавить("/Cfast8УдалитьПатчи")
    
    ЗапуститьПлатформу(ПараметрыЗапуска, ПараметрыПлатформы)
;

@Глобально
метод ПодтвердитьЛегальностьОбновления(ПараметрыПодключения: Соответствие<Строка, Строка>)
    
    пер ОткрыватьКонфигуратор: Булево = Ложь
    пер ИмяОперации: Строка = "Подтверждение легальности"

    пер ПараметрыЗапуска = ПолучитьПараметрыЗапуска(ПараметрыПодключения, ОткрыватьКонфигуратор, ИмяОперации)
    
    пер ПараметрыПлатформы = ПолучитьПараметрыПлатформы(ОткрыватьКонфигуратор, ПараметрыПодключения)
    ПараметрыПлатформы.Добавить("/Execute fast8UpdateHelper.epf")
    ПараметрыПлатформы.Добавить("/Cfast8ОтключитьМодальноеОкно")
    
    ЗапуститьПлатформу(ПараметрыЗапуска, ПараметрыПлатформы)
;

@Глобально
метод ОтключитьБазуОтХранилища(ПараметрыПодключения: Соответствие<Строка, Строка>)
    
    пер ОткрыватьКонфигуратор: Булево = Истина
    пер ИмяОперации: Строка = "Отключение от хранилища конфигурации"

    пер ПараметрыЗапуска = ПолучитьПараметрыЗапуска(ПараметрыПодключения, ОткрыватьКонфигуратор, ИмяОперации)

    пер ПараметрыПлатформы = ПолучитьПараметрыПлатформы(ОткрыватьКонфигуратор, ПараметрыПодключения)
    ПараметрыПлатформы.Добавить("/ConfigurationRepositoryF " + ПараметрыПодключения["КаталогХранилища"])
    ПараметрыПлатформы.Добавить("/ConfigurationRepositoryN " + ПараметрыПодключения["ИмяПользователяХранилища"])
    ПараметрыПлатформы.Добавить("/ConfigurationRepositoryUnbindCfg")
    
    ЗапуститьПлатформу(ПараметрыЗапуска, ПараметрыПлатформы)
;

@Глобально
метод ЗагрузитьКонфигурацию(ПараметрыПодключения: Соответствие<Строка, Строка>, ПутьКФайлу: Строка)
    
    пер ОткрыватьКонфигуратор: Булево = Истина
    пер ИмяОперации: Строка = "Загрузка конфигурации %ПутьКФайлу в базу"

    пер ПараметрыЗапуска = ПолучитьПараметрыЗапуска(ПараметрыПодключения, ОткрыватьКонфигуратор, ИмяОперации)

    пер ПараметрыПлатформы = ПолучитьПараметрыПлатформы(ОткрыватьКонфигуратор, ПараметрыПодключения)
    ПараметрыПлатформы.Добавить("/LoadCfg %ПутьКФайлу")
    ПараметрыПлатформы.Добавить("/UpdateDBCfg -Server")
    ЗапуститьПлатформу(ПараметрыЗапуска, ПараметрыПлатформы)
;

@Глобально
метод ПодключитьБазуКХранилищу(ПараметрыПодключения: Соответствие<Строка, Строка>)
    
    пер ОткрыватьКонфигуратор: Булево = Истина
    пер ИмяОперации: Строка = "Подключение к хранилищу конфигурации"

    пер ПараметрыЗапуска = ПолучитьПараметрыЗапуска(ПараметрыПодключения, ОткрыватьКонфигуратор, ИмяОперации)

    пер ПараметрыПлатформы = ПолучитьПараметрыПлатформы(ОткрыватьКонфигуратор, ПараметрыПодключения)
    ПараметрыПлатформы.Добавить("/ConfigurationRepositoryF " + ПараметрыПодключения["КаталогХранилища"])
    ПараметрыПлатформы.Добавить("/ConfigurationRepositoryN " + ПараметрыПодключения["ИмяПользователяХранилища"])
    ПараметрыПлатформы.Добавить("/ConfigurationRepositoryBindCfg -forceBindAlreadyBindedUser -forceReplaceCfg")
    
    ЗапуститьПлатформу(ПараметрыЗапуска, ПараметрыПлатформы)
;

@Глобально
метод ЗапуститьПроцедуруОбновления(ПараметрыПодключения: Соответствие<Строка, Строка>)
    
    пер ОткрыватьКонфигуратор: Булево = Ложь
    пер ИмяОперации: Строка = "Запуск процедур обновления релиза"

    пер ПараметрыЗапуска = ПолучитьПараметрыЗапуска(ПараметрыПодключения, ОткрыватьКонфигуратор, ИмяОперации)

    пер ПараметрыПлатформы = ПолучитьПараметрыПлатформы(ОткрыватьКонфигуратор, ПараметрыПодключения)
    ПараметрыПлатформы.Добавить("/Execute fast8UpdateHelper.epf")
    ПараметрыПлатформы.Добавить("/CЗапуститьОбновлениеИнформационнойБазы fast8ЗапуститьОбновление")
    
    ЗапуститьПлатформу(ПараметрыЗапуска, ПараметрыПлатформы)
;

@Глобально
метод ВыполнитьРеструктуризациюБазы(ПараметрыПодключения: Соответствие<Строка, Строка>)

    пер ОткрыватьКонфигуратор: Булево = Истина
    пер ИмяОперации: Строка = "Реструктуризация базы"

    пер ПараметрыЗапуска = ПолучитьПараметрыЗапуска(ПараметрыПодключения, ОткрыватьКонфигуратор, ИмяОперации)
    
    пер ПараметрыПлатформы = ПолучитьПараметрыПлатформы(ОткрыватьКонфигуратор, ПараметрыПодключения)
    ПараметрыПлатформы.Добавить("/IBCheckAndRepair -ReIndex -IBCompression -Rebuild")
    
    ЗапуститьПлатформу(ПараметрыЗапуска, ПараметрыПлатформы)
;

@Локально
метод ПолучитьПараметрыПлатформы(ОткрыватьКонфигуратор: Булево, ПараметрыПодключения: Соответствие<Строка, Строка>): Массив<Строка>

    пер ПараметрыПлатформы = новый Массив<Строка>()
    если ОткрыватьКонфигуратор == Истина
        ПараметрыПлатформы.Добавить("DESIGNER")
    иначе
        ПараметрыПлатформы.Добавить("ENTERPRISE")
    ;
    ПараметрыПлатформы.Добавить(ПараметрыПодключения["СтрокаПодключенияБазы"])
    ПараметрыПлатформы.Добавить("/N " + ПараметрыПодключения["ИмяПользователя"])
    ПараметрыПлатформы.Добавить("/P " + ПараметрыПодключения["ПарольПользователя"])
    ПараметрыПлатформы.Добавить("/UC" + ПараметрыПодключения["КодРазрешения"])
    ПараметрыПлатформы.Добавить("/DisableStartupDialogs")
    ПараметрыПлатформы.Добавить("/DisableStartupMessages")
    ПараметрыПлатформы.Добавить("/Out " + ПараметрыПодключения["ИмяФайлаЛога"])
    
    возврат ПараметрыПлатформы
;

@Локально
метод ЗапуститьПлатформу(ПараметрыЗапуска: Соответствие<Строка, Булево|Строка>, ПараметрыПлатформы: Массив<Строка>)

    пер ВерсияПлатформы: Строка = ПараметрыЗапуска["ВерсияПлатформы"]
    пер ОткрыватьКонфигуратор: Булево = ПараметрыЗапуска["ОткрыватьКонфигуратор"]
    пер ИмяОперации: Строка = ПараметрыЗапуска["ИмяОперации"]
    пер ИмяФайлаЛога: Строка = ПараметрыЗапуска["ИмяФайлаЛога"]

    пер ИсполняемыйФайл: Строка = "1cv8.exe"
    если ОткрыватьКонфигуратор == Ложь
        ИсполняемыйФайл = "1cv8c.exe"
    ;
    пер Платформа = новый ПроцессОс("C:\\Program Files\\1cv8\\%ВерсияПлатформы\\bin\\%ИсполняемыйФайл", ПараметрыПлатформы, Ложь)
    
    пер ВремяСтарта: Время = Время.Сейчас()
    Платформа.Запустить()
    Платформа.ОжидатьЗавершения()
    пер ВремяЗавершения: Время = Время.Сейчас()

    знч ФайлЛога = новый Файл(ИмяФайлаЛога)
    знч Содержимое = ФайлЛога.ОткрытьПотокЧтения().ПрочитатьКакСтроку()
    Консоль.Записать(Содержимое)

    пер Длительность = ВремяЗавершения - ВремяСтарта
    пер РезультатОперации = "Операция %ИмяОперации\т$ВремяСтарта\т$ВремяЗавершения\т%Длительность"
    Консоль.Записать(РезультатОперации)
;

@Локально
метод ПолучитьПараметрыЗапуска(ПараметрыПодключения: Соответствие<Строка, Строка>, ОткрыватьКонфигуратор: Булево, ИмяОперации: Строка): Соответствие<Строка, Булево|Строка>

    пер ПараметрыЗапуска = новый Соответствие<Строка, Булево|Строка>()
    ПараметрыЗапуска.Вставить("ВерсияПлатформы", ПараметрыПодключения["ВерсияПлатформы"])
    ПараметрыЗапуска.Вставить("ИмяФайлаЛога", ПараметрыПодключения["ИмяФайлаЛога"])
    ПараметрыЗапуска.Вставить("ОткрыватьКонфигуратор", ОткрыватьКонфигуратор)
    ПараметрыЗапуска.Вставить("ИмяОперации", ИмяОперации)

    возврат ПараметрыЗапуска
;